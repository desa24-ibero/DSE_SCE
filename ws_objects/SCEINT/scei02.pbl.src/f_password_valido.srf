$PBExportHeader$f_password_valido.srf
global type f_password_valido from function_object
end type

forward prototypes
global function integer f_password_valido (string as_password, ref string as_mensaje)
end prototypes

global function integer f_password_valido (string as_password, ref string as_mensaje);//	f_password_valido
//Recibe: as_password	string
//
//Devuelve:	0 Si el password escrito es incorrecto
//				1 Si el password escrito correcto
//			  -1 Si hubo algún problema grave en procesamiento

//Función que recibe un string y revisa si cumple con las reglas establecidas para contraseñas.
//Reglas:
//	1	-	El password debe ser de 8 caracteres
//	2	-	El password debe iniciar en una letra
//	3	-	El password debe incluir al menos un número
//	4	-	Del 2o al 8o caracter solo pueden haber número o letras
//	5	-	No se permiten caracteres especiales ni Ññ's ni vocales acentuadas ÁáÉéÍíÓóÚú ÀàÈèÌìÒòÙù

integer li_tamanio_minimo = 8, li_tamanio_maximo = 8, li_asc_caracter
integer li_tamanio_password = 0, li_indice_cadena= 0
string ls_password_mayusculas = "", ls_caracter_actual = ""
boolean lb_numero_incluido = false

as_mensaje = ""
li_tamanio_password = LEN(as_password)
IF li_tamanio_password<li_tamanio_minimo THEN
	as_mensaje = "El password es menor al tamaño mínimo permitido["+string(li_tamanio_minimo)+"]"
	RETURN 0
END IF

IF li_tamanio_password>li_tamanio_maximo THEN
	as_mensaje = "El password es mayor al tamaño máximo permitido ["+string(li_tamanio_maximo)+"]"
	RETURN 0
END IF

ls_password_mayusculas = UPPER(as_password)

DO WHILE li_indice_cadena <li_tamanio_password
	li_indice_cadena = li_indice_cadena +1
	ls_caracter_actual = MID(ls_password_mayusculas, li_indice_cadena, 1)
	li_asc_caracter = Asc(ls_caracter_actual)
	
	IF li_indice_cadena = 1 THEN
	// Debe iniciar con letra
	//	A  - Z
	//	64 - 91
		IF not(Asc(ls_caracter_actual) > 64 and Asc(ls_caracter_actual) < 91) THEN
			as_mensaje = "El password debe iniciar con un caracter alfabetico"
			RETURN 0		
		END IF
	END IF
	
	IF li_indice_cadena > 1 THEN
	// Debe incluir un número
	//	0  - 9
	//	48 - 57
		IF Asc(ls_caracter_actual) >= 48 and Asc(ls_caracter_actual) <= 57 THEN
			lb_numero_incluido = true		
		END IF
	END IF
	
	IF not ( (Asc(ls_caracter_actual) > 64 and Asc(ls_caracter_actual) < 91) OR &
				(Asc(ls_caracter_actual) >= 48 and Asc(ls_caracter_actual) <= 57) ) THEN
		as_mensaje = "El password solo debe contener letras y números"
		RETURN 0			
	END IF
LOOP

IF not (lb_numero_incluido) THEN
	as_mensaje = "El password debe contener al menos un número"
	RETURN 0			
END IF

	
RETURN 1

end function

